$DEBUG
      SUBROUTINE CNAT(TS1,TS3,TS5,T14,H14,T3,H3,T,TBUB,HBUB,CPRLIQ,QDSC,
     .                QTPC,QSCC,QTOTC,FSUP,FSUB)
C     ******************************************************************
C     *     SUBROUTINE CNAT - CALCULATES THE CONDENSER HEAT            *
C     *     TRANSFER FOR A NATURAL CONVECTION CONDENSER                *
C     ******************************************************************
C
      REAL MDOTR
      DIMENSION T(1)
      COMMON/PARMS/ICOND,IFRSH,IFREZ,DISP,SPEED,CE,CREF,MDOTR,ETAV,SEFF
      COMMON/HTEXS/CFMC,CFME,CFMF,UAF,ETAC,ETAE,ETAF
      COMMON/CONDEN/UDSC,UTPC,USCC,ATOTC,UACOND
      COMMON/SPECS/DTSUPE,DTSUBC
      COMMON / CNDWAL / UA_FF_CND, UA_FZ_CND, UA_FF_HXS, UA_FZ_HXS,
     .                  Q_CND_FF,  Q_CND_FZ,  Q_HXS_FF,  Q_HXS_FZ,
     .                  CONDF_IN_WALL, CONDZ_IN_WALL
 
      DATA SIGMA/2.04326E-7/
C
C          SEGREGATE THE DESUPERHEATING REGION FOR THE HEAT TRANSFER
C          COEFFICIENT CALCULATION SINCE THE TEMPERATURE DIFFERENCE
C          IS HIGH
C
C          SET UP THE HDEW AND TDEW PARAMETERS.  ACCOUNT FOR
C          A WET GAS ENTERING THE CONDENSER
C
      IF(H3 .LT. H14) THEN
         TDEW = T3
         HDEW = H3
      ELSE
         TDEW = T14
         HDEW = H14
      END IF
C
C          CALCULATE THE RADIATION HEAT TRANSFER HEAT TRANSFER
C          COEFFICIENT USING SMALL DELTA T APPROXIMATION (BLACK BODY)
C          USE THE ARITHMETIC AVERAGE OF TEMPERATURE TO EVALUATE H
C          RADIATION IN THE DESUPERHEATING REGION
C
      TAVE = (T14+TDEW)/2.0
      T1 = TAVE*1.8 - 459.6
      HRAD = 4.*SIGMA*TAVE**3
      HRAD = SIGMA*(TAVE + TS1)*(TAVE**2 + TS1**2)
C
C          CALCULATE THE NATURAL CONVECTION HEAT TRANSFER COEFFICIENT
C
      DELTAT = TAVE - TS1
      IF(DELTAT .LE. 0.0) DELTAT = 0.0001
      DELTA = DELTAT*1.8
      HNAT = 0.19*DELTA**0.33*20.44
C
C          CALCULATE COMBINED AIR-SIDE HEAT TRANSFER COEFFICIENT
C
      UAIR = HRAD + HNAT
      U1 = UAIR*0.04892
C
C          CALCULATE THE HEAT TRANSFER ASSUMING THAT THE AIR SIDE
C          RESISTANCE DOMINATES
C
C          CALCULATE THE AREA NECESSARY TO DESUPERHEAT THE REFRIGERANT
C
      TCND = 0.2*T14 + 0.4*TDEW + 0.4*TBUB
      Q_CND_FF = 1.8*UA_FF_CND*(TCND - TS3)*1.0548
      Q_CND_FZ = 1.8*UA_FZ_CND*(TCND - TS5)*1.0548
 
      TFZ = 0.5*(T(8) + T(9))
      TFF = 0.5*(T(5) + T(12))
      Q_HXS_FF = 1.8*UA_FF_HXS*(TCND - TFF)*1.0548
      Q_HXS_FZ = 1.8*UA_FZ_HXS*(TCND - TFZ)*1.0548
 
      IF(TS5 .LE. -290.0) Q_CND_FZ = 0
      IF(TS5 .LE. -290.0) Q_HXS_FZ = 0
 
      Q_IN_WALL = (Q_CND_FF + Q_CND_FZ)/ATOTC
     .                      + (Q_HXS_FF + Q_HXS_FZ)/ATOTC
 
      QDSNEC = MDOTR*(H14-HDEW)
      ADSNEC = QDSNEC/(UAIR*DELTAT + Q_IN_WALL)
      IF(ADSNEC .GT. ATOTC) ADSNEC = ATOTC
      QDSC = UAIR*ADSNEC*DELTAT + ADSNEC*Q_IN_WALL
      FSUP = ADSNEC/ATOTC
      QTPC = 0.0
      QSCC = 0.0
      QTOTC = QDSC + QTPC + QSCC
      FSUB = 0.0
      UDSC = UAIR
      UACOND = ATOTC*UDSC
      IF(FSUP .EQ. 1.0) RETURN
C
C          CALCULATE THE HEAT TRANSFER COEFFICIENTS FOR THE
C          TWO-PHASE AND SUBCOOLING REGIONS
C
      TAVE = (TDEW+TBUB)/2.0
      T2 = TAVE*1.8 - 459.6
      HRAD = 4.*SIGMA*TAVE**3
      HRAD = SIGMA*(TAVE + TS1)*(TAVE**2 + TS1**2)
C
C          CALCULATE THE NATURAL CONVECTION HEAT TRANSFER COEFFICIENT
C
      DELTAT = TAVE - TS1
      IF(DELTAT .LE. 0.0) DELTAT = 0.0001
      DELTA = DELTAT*1.8
      HNAT = 0.19*DELTA**0.33*20.44
C
C          CALCULATE COMBINED AIR-SIDE HEAT TRANSFER COEFFICIENT
C
      UAIR = HRAD + HNAT
      U2 = UAIR*0.04892
C
C          CALCULATE THE HEAT TRANSFER NECESSARY TO CONDENSE THE
C          REFRIGERANT
C
      QTPNEC = MDOTR*(HDEW-HBUB)
C
C          CALCULATE THE REMAINING SURFACE AREA
C
      ANET = ATOTC - ADSNEC
C
C          CALCULATE THE ACTUAL HEAT TRANSFER IN THE TWO-PHASE REGION
C
      QTPC = UAIR*ANET*DELTAT + ANET*Q_IN_WALL
      IF(QTPC .GT. QTPNEC) QTPC = QTPNEC
C
C          CALCULATE THE AREA IN THE TWO-PHASE REGION
C
      ATPC = QTPC/(UAIR*DELTAT + Q_IN_WALL)
      QSCC = 0.0
      QTOTC = QDSC + QTPC + QSCC
      FSUB = 0.0
      UTPC = UAIR
      UACOND = ATOTC*(FSUP*UDSC + (1.0 - FSUP)*UTPC)
      IF(QTPC .LT. QTPNEC) RETURN
C
C          CALCULATE THE REMAINING SURFACE AREA
C
      ANET = ATOTC - (ADSNEC+ATPC)
C
C          CALCULATE THE HEAT TRANSFER IF THE REFRIGERANT EXITS AT TS1
C
      QSCMAX = MDOTR*CPRLIQ*(TBUB-TS1)
C
C          CALCULATE THE SUBCOOLING HEAT TRANSFER
C
      QSCC = UAIR*ANET*DELTAT + ANET*Q_IN_WALL
      IF(QSCC .GT. QSCMAX) QSCC = QSCMAX
      QTOTC = QDSC + QTPC + QSCC
      FSUB = ANET/ATOTC
      USCC = UAIR
      UACOND = ATOTC*(FSUP*UDSC + FSUB*USCC + (1.0 - FSUP - FSUB)*UTPC)
      RETURN
      END
